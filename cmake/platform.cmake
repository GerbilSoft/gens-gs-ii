# Platform-specific functionality.

# Hack to remove -rdynamic from CFLAGS and CXXFLAGS
# See http://public.kitware.com/pipermail/cmake/2006-July/010404.html
IF(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	SET(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS)
	SET(CMAKE_SHARED_LIBRARY_LINK_CXX_FLAGS)
ENDIF()

# Don't embed rpaths in the executables.
SET(CMAKE_SKIP_RPATH ON)

# If we're not using x86, disable emulation functionality.
# TODO: Port everything from asm to C/C++!
# TODO: Add configuration option.
INCLUDE(CheckSystemX8632)
CHECK_SYSTEM_X86_32(GENS_CPU_X86_32)
IF(GENS_CPU_X86_32)
	SET(GENS_ENABLE_EMULATION 1)
ENDIF(GENS_CPU_X86_32)
IF(NOT GENS_ENABLE_EMULATION)
	MESSAGE(WARNING "System is not X86_32; CPU emulation will be disabled. (FOR DEBUGGING ONLY)")
ENDIF(NOT GENS_ENABLE_EMULATION)

# Common flag variables:
# [common]
# - GENS_C_FLAGS_COMMON
# - GENS_CXX_FLAGS_COMMON
# - GENS_EXE_LINKER_FLAGS_COMMON
# - GENS_SHARED_LINKER_FLAGS_COMMON
# [debug]
# - GENS_C_FLAGS_DEBUG
# - GENS_CXX_FLAGS_DEBUG
# - GENS_EXE_LINKER_FLAGS_DEBUG
# - GENS_SHARED_LINKER_FLAGS_DEBUG
# [release]
# - GENS_C_FLAGS_RELEASE
# - GENS_CXX_FLAGS_RELEASE
# - GENS_EXE_LINKER_FLAGS_RELEASE
# - GENS_SHARED_LINKER_FLAGS_RELEASE
#
# DEBUG and RELEASE variables do *not* include COMMON.
IF(MSVC)
	INCLUDE(cmake/platform/msvc.cmake)
ELSE(MSVC)
	INCLUDE(cmake/platform/gcc.cmake)
ENDIF(MSVC)

# Platform-specific configuration.
IF(WIN32)
	INCLUDE(cmake/platform/win32.cmake)
ENDIF(WIN32)

# Check for Large File Support.
INCLUDE(CheckLargeFileSupport)
CHECK_LARGE_FILE_SUPPORT()
IF(NOT "${LFS_DEFINITIONS}" STREQUAL "")
	SET(GENS_C_FLAGS_COMMON "${GENS_C_FLAGS_COMMON} ${LFS_DEFINITIONS}")
	SET(GENS_CXX_FLAGS_COMMON "${GENS_CXX_FLAGS_COMMON} ${LFS_DEFINITIONS}")
ENDIF()

# Set CMAKE flags.
# TODO: RelWithDebInfo / MinSizeRel?
# Common
SET(CMAKE_C_FLAGS			"${CMAKE_C_FLAGS} ${GENS_C_FLAGS_COMMON}")
SET(CMAKE_CXX_FLAGS			"${CMAKE_CXX_FLAGS} ${GENS_CXX_FLAGS_COMMON}")
SET(CMAKE_EXE_LINKER_FLAGS		"${CMAKE_EXE_LINKER_FLAGS} ${GENS_EXE_LINKER_FLAGS_COMMON}")
SET(CMAKE_SHARED_LINKER_FLAGS		"${CMAKE_SHARED_LINKER_FLAGS} ${GENS_SHARED_LINKER_FLAGS_COMMON}")
# Debug
SET(CMAKE_C_FLAGS_DEBUG			"${CMAKE_C_FLAGS_DEBUG} ${GENS_C_FLAGS_DEBUG}")
SET(CMAKE_CXX_FLAGS_DEBUG		"${CMAKE_CXX_FLAGS_DEBUG} ${GENS_CXX_FLAGS_DEBUG}")
SET(CMAKE_EXE_LINKER_FLAGS_DEBUG	"${CMAKE_EXE_LINKER_FLAGS_DEBUG} ${GENS_EXE_LINKER_FLAGS_DEBUG}")
SET(CMAKE_SHARED_LINKER_FLAGS_DEBUG	"${CMAKE_SHARED_LINKER_FLAGS_DEBUG} ${GENS_SHARED_LINKER_FLAGS_DEBUG}")
# Release
SET(CMAKE_C_FLAGS_RELEASE		"${CMAKE_C_FLAGS_RELEASE} ${GENS_C_FLAGS_RELEASE}")
SET(CMAKE_CXX_FLAGS_RELEASE		"${CMAKE_CXX_FLAGS_RELEASE} ${GENS_CXX_FLAGS_RELEASE}")
SET(CMAKE_EXE_LINKER_FLAGS_RELEASE	"${CMAKE_EXE_LINKER_FLAGS_RELEASE} ${GENS_EXE_LINKER_FLAGS_RELEASE}")
SET(CMAKE_SHARED_LINKER_FLAGS_RELEASE	"${CMAKE_SHARED_LINKER_FLAGS_RELEASE} ${GENS_SHARED_LINKER_FLAGS_RELEASE}")

# Unset temporary variables.
# Common
UNSET(GENS_C_FLAGS_COMMON)
UNSET(GENS_CXX_FLAGS_COMMON)
UNSET(GENS_EXE_LINKER_FLAGS_COMMON)
UNSET(GENS_SHARED_LINKER_FLAGS_COMMON)
# Debug
UNSET(GENS_C_FLAGS_DEBUG)
UNSET(GENS_CXX_FLAGS_DEBUG)
UNSET(GENS_EXE_LINKER_FLAGS_DEBUG)
UNSET(GENS_SHARED_LINKER_FLAGS_DEBUG)
# Release
UNSET(GENS_C_FLAGS_RELEASE)
UNSET(GENS_CXX_FLAGS_RELEASE)
UNSET(GENS_EXE_LINKER_FLAGS_RELEASE)
UNSET(GENS_SHARED_LINKER_FLAGS_RELEASE)
